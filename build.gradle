import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.8'
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def dependencyVersions = [
            'com.squareup.okio:okio:1.13.0',
            'org.bouncycastle:bcpkix-jdk15on:1.57',
            'org.codehaus.groovy:groovy:2.4.11',
            'org.codehaus.groovy:groovy-json:2.4.11',
            'org.codehaus.groovy:groovy-all:2.4.11',
            'org.jetbrains.kotlin:kotlin-reflect:1.1.3',
            'org.jetbrains.kotlin:kotlin-stdlib:1.1.3',
            'org.slf4j:slf4j-api:1.7.25',
            'xerces:xercesImpl:2.6.2',
            'xerces:xmlParserAPIs:2.6.2',
            'xml-apis:xml-apis:1.0.b2'
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
    distributionType = Wrapper.DistributionType.ALL
}
